-- wndSubscriptionRequest window definitions
-- Thomas Parslow (PatRat)
-- patrat@rat-software.com

-- --------------------------------------------------------------------------
--
-- License
--
-- The contents of this file are subject to the Jabber Open Source License
-- Version 1.0 (the "License").  You may not copy or use this file, in either
-- source code or executable form, except in compliance with the License.  You
-- may obtain a copy of the License at http://www.jabber.com/license/ or at
-- http://www.opensource.org/.  
--
-- Software distributed under the License is distributed on an "AS IS" basis,
-- WITHOUT WARRANTY OF ANY KIND, either express or implied.  See the License
-- for the specific language governing rights and limitations under the
-- License.
--
-- Copyrights
-- 
-- Copyright (c) Thomas Parslow 2001 unless otherwise stated
-- 
-- --------------------------------------------------------------------------


--This window is not skinable

sequence JIDs --list of qued subscription requests
JIDs = {}
atom Accepted
Accepted = 0

global procedure wndsubscriptionrequest_Close()
    closeWindow(wndSubscriptionRequest)
end procedure

procedure ShowNext()
    sequence label
    Accepted = 0
    if length(JIDs) != 0 then
        setText(wndSubscriptionRequest,"Subscription Request From: " & JIDs[1])
        label = ""
        if not equal(jabber_rosterGetNick(JIDs[1]),JIDs[1]) then
            label = "("&jabber_rosterGetNick(JIDs[1])&") "
        end if
        
        if jabber_rosterGetSubscriptionTo(JIDs[1]) or jabber_rosterGetAsk(JIDs[1]) = JABBER_ROSTER_ASK_SUBSCRIBE then
            setCheck(wndsubscriptionrequest_AddToRoster,False)
            setVisible(wndsubscriptionrequest_AddToRoster,False)
            label = label & "\n" & jabber_rosterGetNick(JIDs[1]) & " is allready on your roster."
        else
            setCheck(wndsubscriptionrequest_AddToRoster,True)
            setVisible(wndsubscriptionrequest_AddToRoster,True)
        end if
        
        label = JIDs[1] & " " & label & "\nIf you accept this request then this user will be able to see when you are online."
        
        setText(wndsubscriptionrequest_JIDLabel,label)
        
        openWindow(wndSubscriptionRequest,Normal)
    else
        wndsubscriptionrequest_Close()
    end if
    repaintWindow(wndSubscriptionRequest)
end procedure

global procedure wndsubscriptionrequest_Open(sequence JID)
    sequence group
    JIDs = append(JIDs,JID)

    if length(JIDs) = 1 then
        ShowNext()
    end if
end procedure

procedure onclose()
    if length(JIDs) = 0 then return end if --Just In Case (TM)
    if Accepted = 0 then
        jabber_DeclineSubscription(JIDs[1])
    end if
    JIDs = JIDs[2..length(JIDs)]
    ShowNext()
    if length(JIDs) > 0 then
        returnValue(1)
    end if
end procedure

procedure onClick_Accept()
    Accepted = 1
    jabber_AcceptSubscription(JIDs[1])
    if isChecked(wndsubscriptionrequest_AddToRoster) then
        wndaddcontact_Open()
        wndaddcontact_SetJID(JIDs[1])
    end if
    wndsubscriptionrequest_Close()
end procedure

procedure onClick_Decline()
    wndsubscriptionrequest_Close()
end procedure

procedure onClick_Profile()
    --TODO
end procedure

onClose[wndSubscriptionRequest] = routine_id("onclose")
onClick[wndsubscriptionrequest_Accept] = routine_id("onClick_Accept")
onClick[wndsubscriptionrequest_Decline] = routine_id("onClick_Decline")
--onClick[wndsubscriptionrequest_Profile] =   routine_id("onClick_Profile")